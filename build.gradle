plugins {
    id 'java-library'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.21.0'
}

group = 'com.worldhub'
version = '1.0.0'

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.mandarin0016'
            artifactId = 'jwt-security-common'
            from components.java

            pom {
                name.set('JWT library')
                description.set('JWT library')
                url.set('https://github.com/worldhub-org/jwt-security-common')
                inceptionYear.set('2021')

                licenses {
                    license {
                        name.set('Apache-2.0')
                        url.set('https://spdx.org/licenses/Apache-2.0.html')
                    }
                }

                developers {
                    developer {
                        id.set('worldhub')
                        name.set('worldhub-org')
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/worldhub-org/jwt-security-common.git'
                    developerConnection = 'scm:git:ssh://github.com/worldhub-org/jwt-security-common.git'
                    url = 'https://github.com/worldhub-org/jwt-security-common'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy').get().asFile.toURI()
        }
    }
}

signing {
    sign publishing.publications.maven
    useGpgCmd()
}

jreleaser {
    project {
        copyright.set('World Hub')
    }
    deploy {
        maven {
            mavenCentral {
                // configure Sonatype publishing
                // Use the sonatype nested block
                sonatype {
                    active = org.jreleaser.model.Active.ALWAYS
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepositories.add('build/staging-deploy')
                    username = project.properties['jreleaserUsername']
                    password = project.properties['jreleaserPassword']
                    sign = false
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:4.0.0'
    }
}

dependencies {
    api 'jakarta.servlet:jakarta.servlet-api'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-autoconfigure'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.withType(Test) {
    useJUnitPlatform()
}